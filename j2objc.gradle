buildscript {
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath "gradle.plugin.com.github.j2objccontrib.j2objcgradle:j2objc-gradle:0.6.0-alpha"
    }
}

group 'SocialHub'
version '1.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'com.github.j2objccontrib.j2objcgradle'

sourceCompatibility = 1.8

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

// j2objcgradle restriction!!
task wrapper(type: Wrapper) {
    gradleVersion = '2.8'
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}


dependencies {

    // Common Library
    compile('org.apache.commons:commons-lang3:3.7')
    compile('commons-codec:commons-codec:1.11')
    compile('com.google.code.gson:gson:2.8.2')
    compile('org.scribe:scribe:1.3.3')

    // J2ObjC Common Library
    compile('com.github.uakihir0:JLogger:1.1')
    compile('com.github.uakihir0:JHttpClient:1.1.4')

    // J2ObjC Network Library
    compile('com.github.uakihir0.twitter4j:twitter4j-core:4.0.6.15')
    compile('com.github.uakihir0.facebook4j:facebook4j-core:2.4.11.3')
    compile('com.github.uakihir0.jslack:jslack-api-client:1.7.4.1')
    compile('com.github.uakihir0.jslack:jslack-api-model:1.7.4.1')
    compile('com.github.uakihir0:msinstance4j:0.0.3')
    compile('com.github.uakihir0:mastodon4j:0.2.10')
    compile('com.github.uakihir0:jumblr:0.0.13.5')

    // Test
    testCompile('junit:junit:4.12')
}


j2objcConfig {
    autoConfigureDeps true
    skipJ2objcVerification true
    extraLinkerArgs "-liconv"

    translateArgs '--build-closure'
    translateArgs '--swift-friendly'
    translateArgs '--prefixes', 'prefixes.properties'
    extraObjcCompilerArgs '-fembed-bitcode'

    finalConfigure()
}

j2objcTranslate.doLast {
    exec {
        executable "sh"
        args './tool/buildheader.sh'
    }
}

j2objcPodspec.doLast {
    [getPodspecDebug(), getPodspecRelease()].asList().each {

        String text = it.text
                .replace(
                "Pod::Spec.new do |spec|",
                '''Pod::Spec.new do |spec|
  spec.authors = 'Akihiro Urushihara'
  spec.license = 'MIT'
  spec.homepage = 'https://twitter.com/U_Akihir0'
  spec.source = { :git => 'git@bitbucket.org:your/repo.git' }''')
                .replace(
                "icucore",
                "icucore', 'iconv")

        it.write(text)
    }
}