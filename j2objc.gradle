buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'idea'
    id 'maven'
    id 'net.socialhub.j2objccontrib.j2objcgradle' version '0.7.2'
}

apply plugin: 'net.socialhub.j2objccontrib.j2objcgradle'

group 'SocialHub'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.8.1'
}

allprojects {
    repositories {
        maven { url 'https://jitpack.io' }
        mavenCentral()
    }
}

dependencies {

    // Translation Exclusion Library
    implementation('com.google.code.findbugs:jsr305:3.0.2')

    // Common Library
    compile('org.apache.commons:commons-lang3:3.7')
    compile('commons-codec:commons-codec:1.11')
    compile('com.google.code.gson:gson:2.8.2')
    compile('org.scribe:scribe:1.3.3')

    // J2ObjC Common Library
    compile('com.github.uakihir0:JLogger:1.4')
    compile('com.github.uakihir0:JHttpClient:1.1.5')
    compile("com.github.uakihir0:Java-WebSocket:1.4.1.1")

    // J2ObjC Network Library
    compile('com.github.uakihir0.twitter4j:twitter4j-core:4.0.8.4')
    compile('com.github.uakihir0.twitter4j:twitter4j-stream:4.0.8.4')
    compile('com.github.uakihir0:twitter-text:3.0.1.1')
    compile('com.github.uakihir0.facebook4j:facebook4j-core:2.4.11.3')
    compile('com.github.uakihir0.jslack:jslack-api-client:3.2.2.2')
    compile('com.github.uakihir0.jslack:jslack-api-model:3.2.2.2')
    compile('com.github.uakihir0:msinstance4j:0.0.3')
    compile('com.github.uakihir0:mastodon4j:0.2.36')
    compile('com.github.uakihir0:jumblr:0.0.13.10')
    compile('com.github.uakihir0:misskey4j:0.1.19')

    // Test
    testCompile('junit:junit:4.12')
}


j2objcConfig {
    autoConfigureDeps true
    skipJ2objcVerification true

    // Only support 64bits
    // TODO: 'ios_arm64e' should be support
    supportedArchs = ['ios_arm64', 'ios_x86_64']

    translateArgs '--build-closure'
    translateArgs '--swift-friendly'
    translateArgs '--prefixes', 'prefixes.properties'
    extraObjcCompilerArgs '-fembed-bitcode'
    extraLinkerArgs "-liconv"

    finalConfigure()
}

j2objcTranslate.doLast {
    exec {
        executable "sh"
        args './tool/buildheader.sh'
    }
}

j2objcPodspec.doLast {
    [getPodspecDebug(), getPodspecRelease()].asList().each {

        String text = it.text
                .replace(
                        "Pod::Spec.new do |spec|",
                        '''Pod::Spec.new do |spec|
  spec.authors = 'Akihiro Urushihara'
  spec.license = 'MIT'
  spec.homepage = 'https://twitter.com/U_Akihir0'
  spec.source = { :git => 'git@bitbucket.org:your/repo.git' }''')
                .replace(
                        "icucore",
                        "icucore', 'iconv")

        it.write(text)
    }
}